import {floor} from "../math.ab"
import {len} from "std"

pub fun erase(erase_arr, index){
    unsafe $unset {nameof erase_arr}[{index}]$
    unsafe ${nameof erase_arr}=("\$\{{nameof erase_arr}[@]}")$
    return erase_arr
}

pub fun is_array(arr): Bool {
    return (
        arr is [Num]  or
        arr is [Text] or
        arr is [Bool] or
        arr is [Null]
    )
    // why is [Null] a thing
}

// create an array of some specified size and set all its elements to init
pub fun array(size, init) {
    let arr = [init] // sets arr to be the same type as init
    arr = erase(arr, 0) // immediately remove init in case size<=0

    loop _ in 0..size {arr += [init]}

    return arr
}

// arr - Array to splice
// start - INDEX to start from
// end - INDEX to end at (exclusive)
pub fun subarray(arr, start, end) {
    // assume arr is always an array
    start = floor(start)
    end = floor(end)

    if start == end:
        return array(0, arr[0])

    if start < 0: start = len(arr) + start
    if end < 0: end = len(arr) + end - 1

    let count = end - start + 1
    if count < 0: count = 0
    silent unsafe ${nameof arr}=("\$\{{nameof arr}[@]:({start}):({count})}")$
    return arr
}

pub fun clear(arr) {
    return subarray(arr,0,0)
}

pub fun reverse(arr) {
    let r_arr = clear(arr)
    loop a in arr {
        r_arr = [a] + r_arr
    }
    return r_arr
}

pub fun includes(arr, value): Bool {
    loop v in arr {
        if v == value:
            return true
    } 
    return false
}